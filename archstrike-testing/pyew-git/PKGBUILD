# Maintainer: ArchStrike <team@archstrike.org>

_pkgname=pyew
pkgname=pyew-git
pkgver=100.20140827
pkgrel=1
pkgdesc="A python tool for static malware analysis"
arch=('armv6' 'armv7' 'i686' 'x86_64')
url="https://code.google.com/p/pyew/"
license=('GPL2')
groups=('archstrike' 'archstrike-malware')
depends=('python2')
makedepends=('git')
source=("${pkgname}::git+https://github.com/joxeankoret/${_pkgname}")
sha512sums=('SKIP')

pkgver() {
  cd ${srcdir}/${pkgname}
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --format=%cd --date=short | tr -d -)"
}

prepare(){
  grep -iRl 'python' "${srcdir}/${pkgname}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
cd "${pkgname}"

  install -dm755 "${pkgdir}/usr/share/${pkgname}/"
  install -dm755 "${pkgdir}/usr/bin/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}/"
  ln -sf /usr/share/${pkgname}/${_pkgname}.py "${pkgdir}/usr/bin/${_pkgname}"
}
